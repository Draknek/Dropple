
package
{
	import flash.display.DisplayObject;
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.utils.getDefinitionByName;
	import flash.events.MouseEvent;

	[SWF (width=640, height=480, frameRate=60)]

	public class Preloader extends Sprite
	{
		private var preloader : Sprite;
		
		private var circlesLayer : Sprite;
		
		public var circles: Array;
		
		private var mouseControl: MouseControl;
		
		private var nextPercentage: int = 0;
		
		public function Preloader()
		{
			if (this.loaderInfo.bytesLoaded < this.loaderInfo.bytesTotal)
			{
				preloader = new Sprite();
				
				preloader.graphics.beginFill(0xFFFFFF)
				preloader.graphics.drawRect(0, 0, 640, 480);
				preloader.graphics.endFill();
				
				circles = [];
				
				circlesLayer = new Sprite();
				
				preloader.addChild(circlesLayer);
				
				mouseControl = new MouseControl();
				
				mouseControl.init();
				
				preloader.addChild(mouseControl);
				
				addChild(preloader);
				
				stage.addEventListener(MouseEvent.MOUSE_DOWN, MouseControl.onMouseDown);
				stage.addEventListener(MouseEvent.MOUSE_UP, MouseControl.onMouseUp);
				
				addEventListener(Event.ENTER_FRAME, checkProgress);
			}
			else
			{
				startup();
			}
		}

		private function checkProgress(e:Event):void
		{
			var p:Number = (this.loaderInfo.bytesLoaded / this.loaderInfo.bytesTotal) * 100;
			
			if (p >= nextPercentage)
			{
				var c: Circle = new Circle();
				
				c.radius *= 1.5;
				c.scaleX = 1.5;
				c.scaleY = 1.5;
				
				c.vy *= 5;
				
				var text: MyTextField = new MyTextField(0, 0, nextPercentage + "%", "center", 14);
				
				text.y = -text.height * 0.33;
				
				if (c.id == 2)
				{
					text.textColor = 0xFFFFFF;
				}
				
				c.addChild(text);
				
				circles.push(c);
				
				circlesLayer.addChild(c);
				
				nextPercentage += 10;
			}
			
			if (p >= 100)
			{
				removeEventListener(Event.ENTER_FRAME, checkProgress);
				startup();
			}
			
			mouseControl.update(circles);
			
			for each (var circle: Circle in circles)
			{
				circle.update(20);
			}
			
			CollisionManager.update(circles);
			
			mouseControl.draw();
			
			for each (circle in circles)
			{
				circle.draw();
			}
		}

		private function startup():void
		{
			removeChild(preloader);
			
			var mainClass:Class = getDefinitionByName("Wsaf") as Class;
			addChild(new mainClass() as DisplayObject);
		}
	}

}


